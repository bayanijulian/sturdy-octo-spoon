<!DOCTYPE HTML> 
<html lang="en"> 
<head>
    <title>MP2 - bjulia2</title>
    <meta charset="utf-8"> 


    <script id="shader-blinn-phong-vs" type="x-shader/x-vertex">
        attribute vec3 aVertexNormal;
        attribute vec3 aVertexPosition;

        varying vec3 vNormal;
        varying vec3 vPosition;
        varying vec3 colorByHeight;

        uniform mat4 uMVMatrix;
        uniform mat4 uPMatrix;
        uniform mat3 uNMatrix;

        uniform vec4 uHeightInterval;
        uniform vec3 uTopColor;
        uniform vec3 uMidColor;
        uniform vec3 uBaseColor;
        uniform vec3 uBotColor;

        void main(void) {
            // Get the vertex position in eye coordinates
            vec4 vertexPositionEye4 = uMVMatrix * vec4(aVertexPosition, 1.0);
            vPosition = vertexPositionEye4.xyz / vertexPositionEye4.w;

            // Calculate the normal
            vNormal = normalize(uNMatrix * aVertexNormal);
            
            // Calculates the color based on height
            if (aVertexPosition.z > uHeightInterval[0]) {
                // snow color
                colorByHeight = uTopColor;
            } else if (aVertexPosition.z > uHeightInterval[1]){
                // earth tone brown
                colorByHeight = uMidColor;
            } else if (aVertexPosition.z > uHeightInterval[2]) {
                // green vegetation color
                colorByHeight = uBaseColor;
            } else {
                // ocean blue waters
                colorByHeight = uBotColor;
            }

            gl_Position = uPMatrix*uMVMatrix*vec4(aVertexPosition, 1.0);
        }
    </script>
        
    <script id="shader-blinn-phong-fs" type="x-shader/x-fragment">
        precision mediump float;
        varying vec3 vNormal;
        varying vec3 vPosition;
        varying vec3 colorByHeight;

        uniform vec3 uLightPosition;
        uniform vec3 uAmbientLightColor;
        uniform vec3 uDiffuseLightColor;
        uniform vec3 uSpecularLightColor;

        uniform vec3 uKAmbient;
        uniform vec3 uKDiffuse;
        uniform vec3 uKSpecular;

        uniform float uShininess;

        void main(void) {
            // Calculate the vector (l) to the light source
            vec3 vectorToLightSource = normalize(uLightPosition - vPosition);
            vec3 normalEye = normalize(vNormal);

            // Calculate n dot l for diffuse lighting
            float diffuseLightWeightning = max(dot(normalEye, 
                                            vectorToLightSource), 0.0);

            // Calculate the reflection vector (r) that is needed for specular light
            vec3 reflectionVector = normalize(reflect(-vectorToLightSource, normalEye));

            // The camera in eye coordinates is located in the origin and is pointing
            // along the negative z-axis. Calculate viewVector (v) 
            // in eye coordinates as:
            // (0.0, 0.0, 0.0) - vertexPositionEye3
            vec3 viewVectorEye = -normalize(vPosition);
            
            //float rdotv = max(dot(reflectionVector, viewVectorEye), 0.0);
            //float specularLightWeightning = pow(rdotv, uShininess);

            vec3 halfwayvec = normalize(vectorToLightSource + viewVectorEye);
            float ndoth = max(dot(normalEye, halfwayvec), 0.0);
            float blinnSpecularLightWeighting = pow(ndoth, uShininess);
            
            // fog shader code
            float fogCoord = (gl_FragCoord.z / gl_FragCoord.w);
            vec4 fogColor = vec4(1.0,1.0,1.0,1.0);

            float LOG2 = 1.442695;
            float fogDensity = 0.5;
            float fogFactor = exp2( -fogDensity * fogDensity * fogCoord * fogCoord * LOG2 );
            fogFactor = clamp(fogFactor, 0.0, 1.0);

            // Sum up all three reflection components and send to the fragment shader
            // uses the color mapping by height
            vec4 fragColor =  vec4(((uAmbientLightColor*uKAmbient)
                    + (uDiffuseLightColor*colorByHeight) * diffuseLightWeightning
                    + ((uSpecularLightColor*uKSpecular) * blinnSpecularLightWeighting)),1.0);
            gl_FragColor = mix(fogColor, fragColor, fogFactor);
        }
    </script>

    <script src="gl-matrix-min.js"></script>
    <script src="webgl-utils.js"></script>
    <script src="Terrain.js"></script>
    <script src="mp2.js"></script>
</head>

<body onload="startup();">
 <canvas id="myGLCanvas" width="800" height="800"></canvas>
    <form>
        <fieldset>
            <legend>Rendering Parameters</legend>
         <input type="radio" name="renderType" id="wireframe" value="wireframe"> Wireframe
         <input type="radio" name="renderType" id="polygon" id="polygon" value="polygon" checked> Polygon
        </fieldset>
    </form>
</body>

</html>
